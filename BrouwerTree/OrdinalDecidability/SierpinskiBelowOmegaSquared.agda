{-# OPTIONS --cubical --guardedness --erasure #-}
module BrouwerTree.OrdinalDecidability.SierpinskiBelowOmegaSquared where

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Function
open import Cubical.Foundations.Structure
open import Cubical.Data.Empty as âŠ¥ using (rec)
open import Cubical.Data.Unit
open import Cubical.Data.Nat using (â„•; zero; suc; predâ„•; _â‰¡áµ‡_; â‰¡áµ‡â†’â‰¡)
                             renaming (_+_ to _N+_)
open import Cubical.Data.Sigma hiding (âˆƒ; âˆƒ-syntax)
open import Cubical.Data.Bool
 using (Bool; true; false; isSetBool; falseâ‰¢true; _â‰Ÿ_; Boolâ†’Type)
open import Cubical.Relation.Nullary.Base using (Dec; yes; no)

open import General-Properties
open import Iff
open import PropTrunc

open import BrouwerTree.Base
open import BrouwerTree.Properties
open import BrouwerTree.Code.Results
open import BrouwerTree.Decidability.Finite
open import BrouwerTree.Arithmetic
open import BrouwerTree.Arithmetic.Properties

open import BrouwerTree.OrdinalDecidability.Basic
open import BrouwerTree.OrdinalDecidability.QuantificationTheorem
open import BrouwerTree.OrdinalDecidability.Sierpinski

private
  variable
    â„“ â„“' : Level

-- For a cantor normal form below Ï‰Â² (written as Ï‰Â·n + k) and any
-- Brouwer ordinal x, it is ğ•Š-decidable whether x â‰¥ Ï‰Â·n+k.

below-Ï‰Â²-sdec : (x : Brw) â†’ (n k : â„•) â†’ ğ•Š-dec (Ï‰ Â· Î¹ n + Î¹ k â‰¤ x)
below-Ï‰Â²-sdec x zero k =
 decâ†’ğ•Š-dec _ (subst (Î» - â†’ Dec (- â‰¤ x)) (sym (zero+yâ‰¡y (Î¹ k))) (dec-nâ‰¤ x k))
below-Ï‰Â²-sdec zero (suc n) k = âŠ¥ , âŠ¥.rec âˆ˜ [1] k , âŠ¥.rec âˆ˜ âŠ¥â‰ âŠ¤
 where
  [1] : (k : â„•) â†’ (a : (limit (Î» m â†’ Ï‰ Â· Î¹ n + Î¹ m) + Î¹ k) â‰¤ zero) â†’ âŠ¥.âŠ¥
  [1] zero = limâ‰°zero
  [1] (suc k) = succâ‰°zero
below-Ï‰Â²-sdec (succ x) (suc n) zero =
 âŒŠ below-Ï‰Â²-sdec x (suc n) zero âŒ‹ ,
 (Ï‰ Â· Î¹ (suc n) â‰¤ succ x
   â†”âŸ¨ limâ‰¤sxâ†”limâ‰¤x (Î» k â†’ Ï‰ Â· Î¹ n + Î¹ k) x âŸ©
  Ï‰ Â· Î¹ (suc n) â‰¤ x
   â†”âŸ¨ âŒŠ below-Ï‰Â²-sdec x (suc n) zero âŒ‹-correct âŸ©
  âŒŠ below-Ï‰Â²-sdec x (suc n) zero âŒ‹ â‰¡ âŠ¤ â†”âˆ)
below-Ï‰Â²-sdec (succ x) (suc n) (suc k) =
 âŒŠ below-Ï‰Â²-sdec x (suc n) k âŒ‹ ,
 ((succ (Ï‰ Â· Î¹ (suc n) + Î¹ k) â‰¤ succ x)
   â†”âŸ¨ â‰¤-succ-mono-â†” âŸ©
  ((Ï‰ Â· Î¹ (suc n) + Î¹ k) â‰¤ x)
   â†”âŸ¨ âŒŠ below-Ï‰Â²-sdec x (suc n) k âŒ‹-correct âŸ©
  âŒŠ below-Ï‰Â²-sdec x (suc n) k âŒ‹ â‰¡ âŠ¤ â†”âˆ)
below-Ï‰Â²-sdec x@(limit f {fâ†‘}) (suc n) zero = Î³
 module Mâ‚€ where
  Î³ = â‹ (Î» k â†’ âŒŠ below-Ï‰Â²-sdec (f k) n 0 âŒ‹) ,
      (Ï‰ Â· Î¹ (suc n) â‰¤ limit f
        â†”âŸ¨ limâ‰¤limâ†”weakly-bisimilar (Î» k â†’ Ï‰ Â· Î¹ n + Î¹ k) f âŸ©
       ((m : â„•) â†’ âˆƒ[ k âˆˆ â„• ] Ï‰ Â· Î¹ n + Î¹ m â‰¤ f k)
        â†”âŸ¨ [1] , [2] âŸ©
       âˆƒ[ k âˆˆ â„• ] Ï‰ Â· Î¹ n â‰¤ f k
        â†”âŸ¨ âˆ¥-âˆ¥â†” (map-snd (Î» {k} â†’ lr âŒŠ below-Ï‰Â²-sdec (f k) n 0 âŒ‹-correct) ,
                 map-snd (Î» {k} â†’ rl âŒŠ below-Ï‰Â²-sdec (f k) n 0 âŒ‹-correct)) âŸ©
       âˆƒ[ k âˆˆ â„• ] âŒŠ below-Ï‰Â²-sdec (f k) n 0 âŒ‹ â‰¡ âŠ¤
        â†”âŸ¨ â†”-sym (is-âŠ¤â†”contains-âŠ¤ (Î» k â†’ âŒŠ below-Ï‰Â²-sdec (f k) n 0 âŒ‹)) âŸ©
       â‹ (Î» k â†’ âŒŠ below-Ï‰Â²-sdec (f k) n 0 âŒ‹) â‰¡ âŠ¤ â†”âˆ)
       where
        [1] : ((m : â„•) â†’ âˆƒ[ k âˆˆ â„• ] Ï‰ Â· Î¹ n + Î¹ m â‰¤ f k)
            â†’ âˆƒ[ k âˆˆ â„• ] Ï‰ Â· Î¹ n â‰¤ f k
        [1] p = p 0
        [2] : âˆƒ[ k âˆˆ â„• ] Ï‰ Â· Î¹ n â‰¤ f k â†’ (m : â„•) â†’ âˆƒ[ k âˆˆ â„• ] Ï‰ Â· Î¹ n + Î¹ m â‰¤ f k
        [2] p m = âˆ¥-âˆ¥map (Î» (k , l) â†’ (k N+ m) , â‰¤-offset-by-Î¹ f {fâ†‘} (Ï‰ Â· Î¹ n) {k} {m} l) p
below-Ï‰Â²-sdec x@(limit f) (suc n) (suc zero) = Î´
 module Mâ‚ where
  Î´ = â‹ (Î» k â†’ âŒŠ below-Ï‰Â²-sdec (f k) (suc n) 1 âŒ‹) ,
      (Ï‰ Â· Î¹ (suc n) < limit f
        â†”âŸ¨ below-limit-â†” (Ï‰ Â· Î¹ (suc n)) f âŸ©
       âˆƒ[ k âˆˆ â„• ] (Ï‰ Â· Î¹ (suc n)) < f k
        â†”âŸ¨ âˆ¥-âˆ¥â†” [1] âŸ©
       âˆƒ[ k âˆˆ â„• ] âŒŠ below-Ï‰Â²-sdec (f k) (suc n) 1 âŒ‹ â‰¡ âŠ¤
        â†”âŸ¨ â†”-sym (is-âŠ¤â†”contains-âŠ¤ (Î» k â†’ âŒŠ below-Ï‰Â²-sdec (f k) (suc n) 1 âŒ‹)) âŸ©
       â‹ (Î» k â†’ âŒŠ below-Ï‰Â²-sdec (f k) (suc n) 1 âŒ‹) â‰¡ âŠ¤ â†”âˆ)
       where
        [1] : (Î£[ k âˆˆ â„• ] (Ï‰ Â· Î¹ (suc n)) < f k)
            â†” (Î£[ k âˆˆ â„• ] âŒŠ below-Ï‰Â²-sdec (f k) (suc n) 1 âŒ‹ â‰¡ âŠ¤)
        [1] = map-snd (Î» {k} â†’ lr (âŒŠ below-Ï‰Â²-sdec (f k) (suc n) 1 âŒ‹-correct)) ,
              map-snd (Î» {k} â†’ rl (âŒŠ below-Ï‰Â²-sdec (f k) (suc n) 1 âŒ‹-correct))
below-Ï‰Â²-sdec x@(limit f) (suc n) (suc (suc k)) =
 âŒŠ below-Ï‰Â²-sdec x (suc n) (suc k) âŒ‹ ,
 (Ï‰ Â· Î¹ (suc n) + Î¹ (suc k) < limit f
   â†”âŸ¨ â†”-sym (x<limâ†”sx<lim f (Ï‰ Â· Î¹ (suc n) + Î¹ k)) âŸ©
  Ï‰ Â· Î¹ (suc n) + Î¹ (suc k) â‰¤ limit f
   â†”âŸ¨ âŒŠ below-Ï‰Â²-sdec (limit f) (suc n) (suc k) âŒ‹-correct âŸ©
  âŒŠ below-Ï‰Â²-sdec (limit f) (suc n) (suc k) âŒ‹ â‰¡ âŠ¤ â†”âˆ)
-- All coherences are automatic, since `ğ•Š-dec ...` is a proposition
below-Ï‰Â²-sdec (bisim f g x i) (suc n) 0 =
  isPropâ†’PathP {B = Î» i â†’ ğ•Š-dec (Ï‰ Â· (Î¹ (suc n)) â‰¤ bisim f g x i)}
               (Î» i â†’ isPropğ•ŠDec isPropâŸ¨â‰¤âŸ©) (Mâ‚€.Î³ f n) (Mâ‚€.Î³ g n) i
below-Ï‰Â²-sdec (bisim f g x i) (suc n) 1 =
 isPropâ†’PathP {B = Î» i â†’ ğ•Š-dec (Ï‰ Â· (Î¹ (suc n)) + Î¹ 1 â‰¤ bisim f g x i)}
              (Î» i â†’ isPropğ•ŠDec isPropâŸ¨â‰¤âŸ©) (Mâ‚.Î´ f n) (Mâ‚.Î´ g n) i
below-Ï‰Â²-sdec (bisim f g x i) (suc n) (suc (suc k)) =
  let Î´ = (âŒŠ below-Ï‰Â²-sdec (limit f) (suc n) (suc k) âŒ‹ ,
          ((succ (limit (Î» nâ‚ â†’ Ï‰ Â· Î¹ n + Î¹ nâ‚) + Î¹ k) < limit f) â†”âŸ¨
           â†”-sym (x<limâ†”sx<lim f (limit (Î» nâ‚ â†’ Ï‰ Â· Î¹ n + Î¹ nâ‚) + Î¹ k)) âŸ©
           (succ (limit (Î» nâ‚ â†’ Ï‰ Â· Î¹ n + Î¹ nâ‚) + Î¹ k) â‰¤ limit f) â†”âŸ¨
           âŒŠ below-Ï‰Â²-sdec (limit f) (suc n) (suc k) âŒ‹-correct âŸ©
           âŒŠ below-Ï‰Â²-sdec (limit f) (suc n) (suc k) âŒ‹ â‰¡ âŠ¤ â†”âˆ))
      Î³ = (âŒŠ below-Ï‰Â²-sdec (limit g) (suc n) (suc k) âŒ‹ ,
          ((succ (limit (Î» nâ‚ â†’ Ï‰ Â· Î¹ n + Î¹ nâ‚) + Î¹ k) < limit g) â†”âŸ¨
           â†”-sym (x<limâ†”sx<lim g (limit (Î» nâ‚ â†’ Ï‰ Â· Î¹ n + Î¹ nâ‚) + Î¹ k)) âŸ©
           (succ (limit (Î» nâ‚ â†’ Ï‰ Â· Î¹ n + Î¹ nâ‚) + Î¹ k) â‰¤ limit g) â†”âŸ¨
           âŒŠ below-Ï‰Â²-sdec (limit g) (suc n) (suc k) âŒ‹-correct âŸ©
           âŒŠ below-Ï‰Â²-sdec (limit g) (suc n) (suc k) âŒ‹ â‰¡ âŠ¤ â†”âˆ)) in
  isPropâ†’PathP
   {B = Î» i â†’ ğ•Š-dec (Ï‰ Â· (Î¹ (suc n)) + Î¹ (suc (suc k)) â‰¤ bisim f g x i)}
   (Î» i â†’ isPropğ•ŠDec isPropâŸ¨â‰¤âŸ©) Î´ Î³ i
below-Ï‰Â²-sdec (trunc x y p q i j) (suc n) k =
 isPropâ†’SquareP' {B = Î» i j â†’ ğ•Š-dec ((Ï‰ Â· Î¹ (suc n) + Î¹ k) â‰¤ trunc x y p q i j)}
                 (Î» i j â†’ isPropğ•ŠDec isPropâŸ¨â‰¤âŸ©)
                 (Î» j â†’ below-Ï‰Â²-sdec x (suc n) k)
                 (Î» j â†’ below-Ï‰Â²-sdec y (suc n) k)
                 (Î» j â†’ below-Ï‰Â²-sdec (p j) (suc n) k)
                 (Î» j â†’ below-Ï‰Â²-sdec (q j) (suc n) k) i j

below-Ï‰Â²-sdec-witness-succ
 : (n k : â„•) (x : Brw)
 â†’ âŒŠ below-Ï‰Â²-sdec (succ x) (suc n) k âŒ‹ â‰¡ âŒŠ below-Ï‰Â²-sdec x (suc n) (predâ„• k) âŒ‹
below-Ï‰Â²-sdec-witness-succ n zero x = refl
below-Ï‰Â²-sdec-witness-succ n (suc k) x = refl

below-Ï‰Â²-sdec-witness-limit
 : (n k : â„•) (f : â„• â†’ Brw) {fâ†‘ : increasing f}
 â†’ âŒŠ below-Ï‰Â²-sdec (limit f {fâ†‘}) (suc n)  (suc k) âŒ‹ â‰¡ âŒŠ below-Ï‰Â²-sdec (limit f {fâ†‘}) (suc n) 1 âŒ‹
below-Ï‰Â²-sdec-witness-limit n zero f = refl
below-Ï‰Â²-sdec-witness-limit n (suc k) f = below-Ï‰Â²-sdec-witness-limit n k f

{-
  As shown by EscardÃ³ and Knapp [1], and formalized by Tom de Jong in [2], the
  following are equivalent:
  - the semidecidable propositions are closed under Î£,
  - the semidecidable propositions form a dominance,
  - a weak choice principle, dubbed* Escardo-Knapp-Choice in [2] and recalled
    below.

  * This should not be confused with a related principle also called
    EscardÃ³-Knapp Choice (EKC) as introduced by Andrew Swan in two talks from
    2019. Swan's choice principle is stronger than the one considered here.
    - https://logic.math.su.se/mloc-2019/slides/Swan-mloc-2019-slides.pdf
    - https://www.math.uwo.ca/faculty/kapulkin/seminars/hottestfiles/Swan-2019-11-06-HoTTEST.pdf

  [1] MartÃ­n H. EscardÃ³ and Cory M. Knapp.
      â€˜Partial Elements and Recursion via Dominances in Univalent Type Theoryâ€™.
      In: 26th EACSL Annual Conference on Computer Science Logic (CSL 2017).
      Ed. by Valentin Goranko and Mads Dam. Vol. 82. Leibniz International
      Proceedings in Informatics (LIPIcs).
      Schloss Dagstuhlâ€“Leibniz-Zentrum fÃ¼r Informatik, 2017, 21:1â€“21:16.
      doi: 10.4230/LIPIcs.CSL.2017.21
  [2] Tom de Jong
      January 2022
      Agda file with comments
      https://cs.bham.ac.uk/~mhe/TypeTopology/NotionsOfDecidability.SemiDecidable.html
-}

Escardo-Knapp-Choice : (â„“ â„“' : Level) â†’ Type (â„“-max (â„“-suc â„“) (â„“-suc â„“'))
Escardo-Knapp-Choice â„“ â„“' = (P : Type â„“) (Q : Type â„“')
                          â†’ isProp P â†’ (P â†’ isProp Q)
                          â†’ semi-dec P â†’ (P â†’ semi-dec Q)
                          â†’ âˆ¥ (P â†’ semi-dec-str Q) âˆ¥

Semidecidable-Closed-Under-Î£ : (â„“ â„“' : Level) â†’ Type (â„“-max (â„“-suc â„“) (â„“-suc â„“'))
Semidecidable-Closed-Under-Î£ â„“ â„“' = (P : Type â„“)
                                  â†’ isProp P
                                  â†’ semi-dec P
                                  â†’ (Q : P â†’ Type â„“')
                                  â†’ ((p : P) â†’ isProp (Q p))
                                  â†’ ((p : P) â†’ semi-dec (Q p))
                                  â†’ semi-dec (Î£ P Q)

Semidecidable-Closed-Under-Countable-Joins : (â„“ : Level) â†’ Type (â„“-suc â„“)
Semidecidable-Closed-Under-Countable-Joins â„“ = (P : â„• â†’ Type â„“)
                                             â†’ ((n : â„•) â†’ isProp (P n))
                                             â†’ ((n : â„•) â†’ semi-dec (P n))
                                             â†’ semi-dec (âˆƒ â„• P)


semidecidable-closed-under-Î£-implies-EKC : Semidecidable-Closed-Under-Î£ â„“ â„“'
                                         â†’ Escardo-Knapp-Choice â„“ â„“'
semidecidable-closed-under-Î£-implies-EKC H P Q P-prop Q-prop Ï Ïƒ = âˆ¥-âˆ¥map g Ï„
 where
  Ï„ : semi-dec (P Ã— Q)
  Ï„ = H P P-prop Ï (Î» _ â†’ Q) Q-prop Ïƒ

  g : semi-dec-str (P Ã— Q) â†’ P â†’ semi-dec-str Q
  g ss p = lr (semi-dec-str-stable-under-â†” (P Ã— Q) Q (snd , Î» q â†’ p , q)) ss

-- The below is adapted from from [2].
semidecidable-closed-under-countable-joins-implies-also-closed-under-Î£
 : Semidecidable-Closed-Under-Countable-Joins (â„“-max â„“ â„“')
 â†’ Semidecidable-Closed-Under-Î£ â„“ â„“'
semidecidable-closed-under-countable-joins-implies-also-closed-under-Î£ {â„“ = â„“} {â„“' = â„“'} H P P-prop Ï Q Q-prop Ïƒ =
 âˆ¥-âˆ¥rec isPropPropTrunc Ï„ Ï
  where
   Ï„ : semi-dec-str P â†’ âˆ¥ semi-dec-str (Î£ P Q) âˆ¥
   Ï„ (Î± , e) = lr (semi-dec-stable-under-â†” (âˆƒ â„• QÌƒ) _ Î£QÌƒ-Î£Q-â†”)
                  (H QÌƒ QÌƒ-is-prop-valued QÌƒ-is-semidecidable)
    where
     abstract
       PÌƒ : â„• â†’ hProp â„“
       PÌƒ n = Lift (Î± n â‰¡ true) , isPropLift (isSetBool _ _)

       PÌƒ-is-decidable : (n : â„•) â†’ Dec (typ (PÌƒ n))
       PÌƒ-is-decidable n with Î± n
       ... | false = no (Î» e â†’ falseâ‰¢true (lower e))
       ... | true = yes (lift refl)

       PÌƒ-property : âˆƒ[ n âˆˆ â„• ] Î± n â‰¡ true â†” (Î£[ n âˆˆ â„• ] typ (PÌƒ n))
       PÌƒ-property = f , g
        where
         f : âˆƒ[ n âˆˆ â„• ] Î± n â‰¡ true â†’ Î£[ n âˆˆ â„• ] typ (PÌƒ n)
         f = map-snd lift âˆ˜ least-witness _ (Î» n â†’ isSetBool _ _) (Î» n â†’ Î± n â‰Ÿ true)
         g : Î£[ n âˆˆ â„• ] typ (PÌƒ n) â†’ âˆƒ[ n âˆˆ â„• ] Î± n â‰¡ true
         g (n , lift e) = âˆ£ n , e âˆ£

       Î£PÌƒ-to-P : (Î£[ n âˆˆ â„• ] typ (PÌƒ n)) â†’ P
       Î£PÌƒ-to-P = rl e âˆ˜ rl PÌƒ-property

     QÌƒ : â„• â†’ Type (â„“-max â„“ â„“')
     QÌƒ n = Î£[ p âˆˆ typ (PÌƒ n) ] Q (Î£PÌƒ-to-P (n , p))

     QÌƒ-is-prop-valued : (n : â„•) â†’ isProp (QÌƒ n)
     QÌƒ-is-prop-valued n = isPropÎ£ (str (PÌƒ n)) (Î» p â†’ Q-prop (Î£PÌƒ-to-P (n , p)))

     Î£QÌƒ-Î£Q-â†” : âˆƒ â„• QÌƒ â†” Î£ P Q
     Î£QÌƒ-Î£Q-â†” = âˆ¥-âˆ¥rec (isPropÎ£ P-prop Q-prop)
                      (Î» (n , pÌƒ , q) â†’ Î£PÌƒ-to-P (n , pÌƒ) , q )
             , Î» (p , q) â†’ âˆ£ fst (lr PÌƒ-property (lr e p)) ,
                             snd (lr PÌƒ-property (lr e p)) ,
                             subst Q (P-prop _ _) q âˆ£

     QÌƒ-is-semidecidable : (n : â„•) â†’ semi-dec (QÌƒ n)
     QÌƒ-is-semidecidable n with PÌƒ-is-decidable n
     ... | yes pÌƒ = lr (semi-dec-stable-under-â†” _ _ claim) (Ïƒ p)
      where
       p : P
       p = Î£PÌƒ-to-P (n , pÌƒ)
       claim : Q p â†” QÌƒ n
       claim = (Î» q â†’ pÌƒ , q) ,
               (Î» (pÌƒ' , q) â†’ subst (Î» - â†’ Q (Î£PÌƒ-to-P (n , -))) (str (PÌƒ n) _ _) q)
     ... | no Â¬pÌƒ = lr (semi-dec-stable-under-â†” _ _ claim)
                      âˆ£ decâ†’semi-dec _ (no (Î» e â†’ e)) âˆ£
      where
       claim : âŠ¥.âŠ¥ â†” QÌƒ n
       claim = âŠ¥.rec , (Î» (pÌƒ , q) â†’ Â¬pÌƒ pÌƒ)

-- We now use the above result to separate ğ•Š-decidability and
-- semidecidability.

ğ•Š-decidable-closed-under-countable-joins : (P : â„• â†’ Type â„“)
                                         â†’ ((n : â„•) â†’ ğ•Š-dec (P n))
                                         â†’ ğ•Š-dec (âˆƒ â„• P)
ğ•Š-decidable-closed-under-countable-joins P Ï =
 â‹ (Î» n â†’ âŒŠ Ï n âŒ‹) , (âˆƒ[ n âˆˆ â„• ] P n          â†”âŸ¨ âˆ¥-âˆ¥â†” (map-snd (lr (âŒŠ Ï _ âŒ‹-correct)) ,
                                                       map-snd (rl (âŒŠ Ï _ âŒ‹-correct))) âŸ©
                      âˆƒ[ n âˆˆ â„• ]  âŒŠ Ï n âŒ‹ â‰¡ âŠ¤ â†”âŸ¨ â†”-sym (is-âŠ¤â†”contains-âŠ¤ (Î» n â†’ âŒŠ Ï n âŒ‹)) âŸ©
                      â‹ (Î» n â†’ âŒŠ Ï n âŒ‹) â‰¡ âŠ¤       â†”âˆ)

ğ•Š-decâ†’Semi-dec-implies-semidecidable-closed-under-Î£
 : ((P : Type â„“) â†’ ğ•Š-dec P â†’ semi-dec P)
 â†’ Semidecidable-Closed-Under-Countable-Joins â„“
ğ•Š-decâ†’Semi-dec-implies-semidecidable-closed-under-Î£ H P P-is-Prop Ï =
 H (âˆƒ â„• P) (ğ•Š-decidable-closed-under-countable-joins
             P
             (Î» n â†’ semidecâ†’ğ•Š-dec (P n) (P-is-Prop n) (Ï n)))

Ï‰3â‰¤x-semidec-implies-semidecidable-closed-under-countable-joins
 : ((x : Brw) â†’ semi-dec (Ï‰ Â· Î¹ 3 â‰¤ x))
 â†’ Semidecidable-Closed-Under-Countable-Joins â„“
Ï‰3â‰¤x-semidec-implies-semidecidable-closed-under-countable-joins H P P-is-prop Ï =
 âˆ¥-âˆ¥map (lr (semi-dec-str-stable-under-â†” _ _ (â†”-sym Ïƒ))) (H x)
  where
   x = fst (Pâ‚™-Ï‰decâ†’âˆƒnPâ‚™-Ï‰Â·3dec' P P-is-prop Ï)
   Ïƒ = snd (Pâ‚™-Ï‰decâ†’âˆƒnPâ‚™-Ï‰Â·3dec' P P-is-prop Ï)

Ï‰3â‰¤x-semidec-implies-EKC : ((x : Brw) â†’ semi-dec (Ï‰ Â· Î¹ 3 â‰¤ x)) â†’ Escardo-Knapp-Choice â„“ â„“'
Ï‰3â‰¤x-semidec-implies-EKC =
 semidecidable-closed-under-Î£-implies-EKC âˆ˜
 semidecidable-closed-under-countable-joins-implies-also-closed-under-Î£ âˆ˜
 Ï‰3â‰¤x-semidec-implies-semidecidable-closed-under-countable-joins
